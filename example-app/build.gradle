description = 'A demo application using the trafficsoft datahub module'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'org.springframework.boot'

configurations.all {
    resolutionStrategy {
        preferProjectModules()
        failOnVersionConflict()

        dependencySubstitution {
            // necessary as spring-boot plugin pulls in old versions
            // see https://discuss.gradle.org/t/excluded-dependence-comes-back-when-spring-boot-plugin-is-applied/17945/2
            substitute module('com.zaxxer:HikariCP') with module("com.zaxxer:HikariCP:${hikariVersion}")
            substitute module('org.flywaydb:flyway-core') with module("org.flywaydb:flyway-core:${flywayVersion}")
            substitute module('org.xerial:sqlite-jdbc') with module("org.xerial:sqlite-jdbc:${sqliteJdbcVersion}")
            substitute module('io.projectreactor:reactor-core') with module("io.projectreactor:reactor-core:${reactorVersion}")
        }
    }
}

bootRun {
    systemProperties = project.gradle.startParameter.systemPropertiesArgs
    addResources = true
}

bootRepackage {
    executable = true
    mainClass = 'org.amv.trafficsoft.datahub.example.TrafficsoftDataHubApplication'
}

springBoot {
    executable = true
}

dependencies {
    compile "org.amv.trafficsoft.datahub:xfcd-spring-boot-starter:${amvDatahubVersion}"
    compile "org.amv.trafficsoft.datahub:xfcd-consumer-sqlite-spring-boot-starter:${amvDatahubVersion}"
    compile "org.amv.trafficsoft.datahub:xfcd-consumer-mysql-spring-boot-starter:${amvDatahubVersion}"

    compile "org.xerial:sqlite-jdbc:${sqliteJdbcVersion}"
    compile "mysql:mysql-connector-java:${mysqlConnectorVersion}"

    compile("org.springframework.boot:spring-boot-starter:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
}